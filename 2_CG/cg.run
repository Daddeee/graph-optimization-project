
reset;
model cg.mod;
data ../Assignment/instances/i_10_1.dat;


problem master;
option solver '/home/davide/ampl_linux-intel64/cplex';

problem pricing;
option solver '/home/davide/ampl_linux-intel64/cplex';

#auxiliary parameters
param flag default 0;
param max_iter default 100;

let M:= N;

for{i in 1..N}{
	
	let s[i,i] := 1;

}


printf "\n \t \t \t COLUMN GENERATION \n\n\n" > results.txt;
printf"------------------------------------------------ \n" > results.txt;

for{counter in 1 .. max_iter} 
{     
	#reset flag
	let flag := 0;
	     
    #solve the current master problem   
	solve master;
	
	#print the optimal objective value of the current master problem 
	printf"\n objective function (bins): %7.6f\n", bin_CG > results.txt;
	printf"\n used bins: "> results.txt;
	for{binindex in 1..M}{
		if (l[binindex] > 0) then{
			printf"\n virtual machine %d (%g)", binindex, l[binindex] > results.txt;
			printf"\t requests: "> results.txt;
			for{itemindex in 1..N}{
				if(s[itemindex, binindex] == 1) then{
					printf" %d \t", itemindex > results.txt;
				}
			}
		}
	
	}
	printf"\n\n"> results.txt;
	
	
	#set the profit of each item equal to
	#the corresponding dual variable of the current master problem	
	for{i in 1..N} {
		if(cover[i].dual <=0) then let pi[i] := 0;
		else{
			let pi[i] := cover[i].dual;
		}
	}
		
	#solve the current pricing problem
	solve pricing;
	
	#print the cluster selected by the current pricing problem	
	printf "\n Candidate new virtual machine: \t" > results.txt;	
	for{i in 1..N}
	{
		if (u[i] = 1) then
			printf "%d  ",i > results.txt;
	}
	
	#print the profit of the candidate new cluster
	printf "\n profit: %7.6f \n", profit > results.txt;
	
	#check if the candidate new cluster makes the current dual solution infeasible
	if  (profit <= 1) then
	{
		#current master problem solution optimal -> set flag to 0	
		let flag := 0;
		
		printf "\n Did not add the virtual machine to the master problem \n \n" > results.txt;
		printf"------------------------------------------------ \n" > results.txt;
		
		#terminate the column generation procedure
		break;
	}
	else
	{			
		#add the candidate new cluster (column/variable) to the master problem				
		let M:=M+1;	    			
		for{i in 1..N}
			let s[i,M]:= u[i];
		
		#print the current number of clusters in the master problem
	 	printf "\n Added virtual machine\n"> results.txt;
	    printf" M=%d \n", M > results.txt;
		printf" \n" > results.txt;
		printf"------------------------------------------------ \n" > results.txt;
		
		#current master problem solution not optimal -> set flag to 1	
		let flag:= 1;	
			 	
	}
}

#solve the master problem with the final set of clusters (columns/variables)
solve master;

#print the optimal objective function value of the final master problem
printf "\n final master objective: %7.6f \n", bin_CG > results.txt;
#print the number of columns (variables) of the final master problem	
printf "\n final number of columns: %7d \n", M > results.txt;

#check if the column generation procedure is forced to terminate
#before finding the optimal solution 
if (flag=1) then
	printf"\n Solution is not optimal: increase number of iterations!\n" > results.txt;	
else
	printf"\n Solution is optimal!\n" > results.txt;	
